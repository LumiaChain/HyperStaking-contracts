// SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.27;

import {UserPoolInfo, StakingPoolInfo} from "../libraries/LibStaking.sol";

/**
 * @title IStakingFacet
 * @dev Interface for StakingFacet
 */
interface IStakingFacet {
    //============================================================================================//
    //                                          Events                                            //
    //============================================================================================//

    event StakeDeposit(
        address indexed from,
        address indexed to,
        uint256 indexed poolId,
        address strategy,
        uint256 amount
    );

    event StakeWithdraw(
        address indexed from,
        address indexed to,
        uint256 indexed poolId,
        address strategy,
        uint256 amount,
        uint256 withdrawAmount
    );

    event StakingPoolCreate(
        address indexed from,
        address indexed stakeToken,
        uint256 idx,
        uint256 poolId
    );

    //============================================================================================//
    //                                          Errors                                            //
    //============================================================================================//

    /// @notice Thrown when the provided eth value is incorrect
    error DepositBadValue();

    /// @dev TODO remove
    error Unsupported();

    /// @notice Thrown when failed to transfer ETH value (with call)
    error WithdrawFailedCall();

    /// @notice Thrown when attempting to access a non-existent staking pool
    error PoolDoesNotExist();

    /// @notice Thrown when attempting to deposit to a non-existent vault
    error VaultDoesNotExist();

    //============================================================================================//
    //                                          Mutable                                           //
    //============================================================================================//

    /**
     * @notice Initializes the contract, setting up necessary state variables, // TODO remove
     */
    function init() external;

    /**
     * @notice Deposits a specified amount into a staking pool with a chosen strategy
     * @param poolId The ID of the staking pool to deposit into
     * @param strategy The address of the strategy selected by the user
     * @param amount The amount of the token to stake
     * @param to The address receiving the staked token allocation (typically the user's address)
     */
    function stakeDeposit(
        uint256 poolId,
        address strategy,
        uint256 amount,
        address to
    ) external payable;

    /**
     * @notice Withdraws a specified amount from a staking pool, including revenue generated by
     *         the strategy
     * @param poolId The ID of the staking pool to withdraw from
     * @param strategy The address of the strategy used during staking
     * @param amount The amount of the staked token to withdraw
     * @param to The address to receive the withdrawn tokens
     * @return withdrawAmount The total amount withdrawn, including the initial stake
     *         and any generated revenue
     * @dev withdrawAmount = initial stake amount + revenue generated by the strategy
     */
    function stakeWithdraw(
        uint256 poolId,
        address strategy,
        uint256 amount,
        address to
    ) external returns (uint256 withdrawAmount);

    //============================================================================================//
    //                                           View                                             //
    //============================================================================================//

    /**
     * @notice Returns information about a user's staking position in a specific pool
     * @param poolId The ID of the staking pool
     * @param user The address of the user
     * @return A struct containing the user's staking information
     */
    function userPoolInfo(
        uint256 poolId,
        address user
    ) external view returns (UserPoolInfo memory);

    /**
     * @notice Returns information about a specific staking pool
     * @param poolId The ID of the staking pool
     * @return A struct containing details about the pool
     */
    function poolInfo(uint256 poolId) external view returns (StakingPoolInfo memory);

    /**
     * @notice Returns the user's share of the total pool allocation
     * @param poolId The ID of the staking pool
     * @param user The address of the user
     * @return The user's share of the pool, represented as a scaled percentage
     *         (using 1e18 as a precision factor)
     */
    function userPoolShare(uint256 poolId, address user) external view returns (uint256);

    /**
     * @notice Returns the address of the native token
     * @dev For internal use, an address is generated, e.g., for ETH
     * @return The address of the native token
     */
    function nativeTokenAddress() external pure returns (address);

    /**
     * @notice Generates a unique pool ID for a specific staking token and pool index
     * @param stakeToken The address of the token being staked
     * @param idx The index of the pool for that token (starting from 0)
     * @return The unique ID for the pool
     */
    function generatePoolId(address stakeToken, uint96 idx) external pure returns (uint256);
}
