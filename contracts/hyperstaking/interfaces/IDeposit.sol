// SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.27;

import {DirectStakeInfo} from "../libraries/LibHyperStaking.sol";

/**
 * @title IDeposit
 * @dev Interface for DepositFacet
 */
interface IDeposit {
    enum DepositType {
        Direct,
        Tier1,
        Tier2
    }

    //============================================================================================//
    //                                          Events                                            //
    //============================================================================================//

    event StakeDeposit(
        address from,
        address indexed to,
        address indexed strategy,
        uint256 stake,
        DepositType indexed tier    // 0 - Direct
                                    // 1 - Tier1
                                    // 2 - Tier2
    );

    event StakeWithdraw(
        address from,
        address indexed to,
        address indexed strategy,
        uint256 stake,
        uint256 withdrawAmount,
        DepositType indexed tier
    );

    //============================================================================================//
    //                                          Errors                                            //
    //============================================================================================//

    /// @notice Thrown when attempting to stake zero amount
    error ZeroStake();

    /// @notice Thrown when attempting to stake to disabled strategy
    error StrategyDisabled(address strategy);

    /// @notice Thrown when attempting to deposit to a non-existent vault
    error VaultDoesNotExist(address strategy);

    /// @notice Thrown when depositing to a not direct deposit vault
    error NotDirectDeposit(address strategy);


    //============================================================================================//
    //                                          Mutable                                           //
    //============================================================================================//

    /* ========== Direct Deposit ========== */

    /**
     * @notice Deposits a specified stake amount directly into the lockbox and bridges it to Lumia
     *         Uses a designated Strategy contract, which does not perform any yield generation
     * @param strategy The address of the strategy associated with the vault
     * @param stake The amount of the token to stake
     * @param to The address receiving the staked token allocation (typically the user's address)
     */
    function directStakeDeposit(
        address strategy,
        uint256 stake,
        address to
    ) external payable;

    /**
     * @notice Withdraws a specified stake
     * @dev Used internally and is called by Lockbox after getting StakeRedeem message
     * @param strategy The address of the strategy associated with the vault
     * @param stake The amount of the staked token to withdraw
     * @param to The address to receive the withdrawn tokens
     */
    function directStakeWithdraw(
        address strategy,
        uint256 stake,
        address to
    ) external returns (uint256 withdrawAmount);

    /* ========== Tier 1  ========== */

    /**
     * @notice Deposits a specified stake amount into Tier1 the chosen strategy
     * @param strategy The address of the strategy selected by the user
     * @param stake The amount of the token to stake
     * @param to The address receiving the staked token allocation (typically the user's address)
     */
    function stakeDepositTier1(
        address strategy,
        uint256 stake,
        address to
    ) external payable;

    /**
     * @notice Withdraws a specified stake amount, including revenue generated by the strategy
     * @param strategy The address of the strategy used during staking
     * @param stake The amount of the staked token to withdraw
     * @param to The address to receive the withdrawn tokens
     * @return withdrawAmount The total amount withdrawn, including the initial stake
     *         and any generated revenue
     * @dev withdrawAmount = initial stake amount + revenue generated by the strategy
     */
    function stakeWithdrawTier1(
        address strategy,
        uint256 stake,
        address to
    ) external returns (uint256 withdrawAmount);

    /* ========== Tier 2  ========== */

    /**
     * @notice Deposits a specified stake amount into Tier2 of the chosen strategy
     * @param strategy The address of the strategy selected by the user
     * @param stake The amount of the token to stake
     * @param to The address receiving the staked token allocation (typically the user's address)
     */
    function stakeDepositTier2(
        address strategy,
        uint256 stake,
        address to
    ) external payable;

    /**
     * @notice Withdraws a specified amount of Tier2 shares, which are then exchanged for stake
     *         and send to the user
     * @dev Used internally and is called by Lockbox after getting StakeRedeem message
     * @param strategy The address of the strategy associated with the vault
     * @param stake The amount of the staked token to withdraw
     * @param to The address to receive the withdrawn tokens
     */
    function stakeWithdrawTier2(
        address strategy,
        uint256 stake,
        address to
    ) external returns (uint256 withdrawAmount);

    /* ========== */

    /// @notice Pauses stake functionalities
    function pauseDeposit() external;

    /// @notice Resumes stake functionalities
    function unpauseDeposit() external;

    //============================================================================================//
    //                                           View                                             //
    //============================================================================================//

    /// @notice Retrieves information about all direct stakes for a specified strategy
    function directStakeInfo(address strategy) external view returns (DirectStakeInfo memory);
}
