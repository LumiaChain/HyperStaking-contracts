// SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.27;

import {RouteInfo} from "../libraries/LibInterchainFactory.sol";
import {LumiaLPToken} from "../LumiaLPToken.sol";

import {IVaultFactory} from "../../external/3adao-lumia/interfaces/IVaultFactory.sol";
import {IVault} from "../../external/3adao-lumia/interfaces/IVault.sol";

/**
 * @title IRouteFactory
 * @dev Interface for RouteFactoryFacet
 */
interface IRouteFactory {
    //============================================================================================//
    //                                          Events                                            //
    //============================================================================================//

    event VaultFactoryUpdated(address oldVaultFactory, address newVaultFactory);
    event LendingPropertiesUpdated(
        address indexed strategy,
        bool enabled,
        uint256 borrowSafetyBuffer
    );

    event TokenDeployed(
        address strategy,
        address lpToken,
        string name,
        string symbol,
        uint8 decimals
    );

    event CollateralBridged(
        address indexed strategy,
        address indexed lpToken,
        address lendingVault,
        address indexed sender,
        uint256 shares,
        uint256 borrowed
    );

    event TokenBridged(
        address indexed strategy,
        address indexed lpToken,
        address indexed sender,
        uint256 shares
    );

    event RwaTokenRedeemed(
        address indexed strategy,
        address lendingVault,
        address indexed lpToken,
        address user,
        uint256 shares,
        uint256 rwaAmount,
        uint256 redeemptionFee
    );

    event VaultYieldCollected(
        address indexed strategy,
        address indexed lendingVault,
        uint256 amount,
        address indexed to
    );

    event VaultOwnershipTransfered(
        address indexed strategy,
        address indexed lendingVault,
        address indexed to
    );

    //===========================================================================================//
    //                                          Errors                                            //
    //============================================================================================//

    error InvalidVaultFactory(address badVaultFactory);
    error InvalidSafetyBuffer();

    error RouteAlreadyExist();
    error RouteDoesNotExist(address strategy);

    //============================================================================================//
    //                                          Mutable                                           //
    //============================================================================================//

    /// @notice Handle specific TokenDeploy message
    function handleTokenDeploy(
        address originLockbox,
        uint32 originDestination,
        bytes calldata data
    ) external;

    /// @notice Handle specific TokenBridge message
    function handleTokenBridge(bytes calldata data) external;


    /**
     * @notice Initiates rwa token redemption
     * @dev Ths function transferFrom rwaToken to repay lending debt
     *      for given amount of lpToken shares
     * @param strategy Address of the strategy
     * @param user Address of the user whose process is initiated
     * @param shares Amount of shares (lpToken) to redeem
     */
    function redeemRwaTokens(
        address strategy,
        address user,
        uint256 shares
    ) external payable;

    /**
     * @notice Updates the vaultFactory address used for creating vaults
     * @param newVaultFactory The new mailbox address
     */
    function setVaultFactory(address newVaultFactory) external;

    /**
     * @notice Updates the lending availability for the specified route strategy
     * @dev Lending is enabled by default
     */
    function updateLendingProperties(
        address strategy,
        bool enabled,
        uint256 borrowSafetyBuffer
    ) external;

    /**
     * @notice Collects yield generated by a specific vault strategy
     * @dev Allows an authorized manager to withdraw yield from a vault strategy
    */
    function collectVaultYield(
        address strategy,
        uint256 amount,
        address to
    ) external;

    /**
     * @notice Transfers ownership of a lending vault
     * @dev Allows an authorized manager to change the owner of a vault strategy
     */
    function transferVaultOwnership(
        address strategy,
        address to
    ) external;

    //============================================================================================//
    //                                           View                                             //
    //============================================================================================//

    /// @notice Retrieves the vaultFactory
    function getVaultFactory() external view returns (IVaultFactory);

    /// @notice Retrieves the lpToken associated with a given strategy
    function getLpToken(address strategy) external view returns (LumiaLPToken);

    /// @notice Retrieves the lendingVault associated with a given strategy
    function getLendingVault(address strategy) external view returns (IVault);
}
